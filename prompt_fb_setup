#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/AzjmpwM.png
#

# Load dependencies.
pmodload 'helper'

function prompt_sorin_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sorin_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sorin_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_fbrepo {
}

function prompt_sorin_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_sorin_pwd

  # Get Git repository information.
  if (( $+functions[_dotfiles_scm_info] )); then
    WANT_OLD_SCM_PROMPT=true

    if [[ -n $(_dotfiles_scm_info) ]]; then
      _scm_info_string=" (%F{yellow}$(_dotfiles_scm_info)%F{blue})"
    else
      _scm_info_string=""
    fi
  fi
}

function prompt_sorin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_sorin_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B$%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %F{red}$%f%b'

  # Define prompts.
  PROMPT='%B%F{red}%n%f@%F{yellow}%m%f %F{blue}${_prompt_sorin_pwd}${_scm_info_string}%f%b%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
#  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}${INSIDE_EMACS:+" %B%F{green}E%f%b"}%B${scm_info_string}'
  SPROMPT='zsh: correct %B%F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_sorin_setup "$@"
